# pyx.zsh-theme
# Based on jonathan and spaceship thems

# PREFIXES
SPACESHIP_PREFIX_SHOW="${SPACEHIP_PREFIX_SHOW:-true}"
SPACESHIP_PREFIX_ENV_DEFAULT="${SPACESHIP_PREFIX_ENV_DEFAULT:-"via "}"
SPACESHIP_PREFIX_VENV="${SPACESHIP_PREFIX_VENV:-$SPACESHIP_PREFIX_ENV_DEFAULT}"
SPACESHIP_PREFIX_PYENV="${SPACESHIP_PREFIX_PYENV:-$SPACESHIP_PREFIX_ENV_DEFAULT}"
# VENV
SPACESHIP_VENV_SHOW="${SPACESHIP_VENV_SHOW:-true}"

# PYENV
SPACESHIP_PYENV_SHOW="${SPACESHIP_PYENV_SHOW:-true}"
SPACESHIP_PYENV_SYMBOL="${SPACESHIP_PYENV_SYMBOL:-üêç}"

functions rbenv_prompt_info >& /dev/null || rbenv_prompt_info(){}


function theme_precmd {
    local TERMWIDTH
    (( TERMWIDTH = ${COLUMNS} - 1 ))


    ###
    # Truncate the path if it's too long.

    PR_FILLBAR=""
    PR_PWDLEN=""

    local promptsize=${#${(%):---(%n@%m:%l)---()--}}
    local rubyprompt=`rvm_prompt_info || rbenv_prompt_info`
    local gitprompt=$(git_prompt_info)$(git_prompt_status)
    local zero='%([BSUbfksu]|([FK]|){*})'
    local pyprompt=$(spaceship_pyenv_status)$(spaceship_venv_status)
    local rubypromptsize=${#${rubyprompt}}
    local gitpromptsize=${#${(S%%)gitprompt//$~zero/}}
    local pypromptsize=${#${(S%%)pyprompt//$~zero/}}
    local pwdsize=${#${(%):-%~}}

    if [[ "$promptsize + $rubypromptsize + $pwdsize" -gt $TERMWIDTH ]]; then
      ((PR_PWDLEN=$TERMWIDTH - $promptsize))
    else
      PR_FILLBAR="\${(l.(($TERMWIDTH - ($promptsize + $rubypromptsize + $pypromptsize + $gitpromptsize + $pwdsize)))..${PR_HBAR}.)}"
    fi

}

# Virtual environment.
# Show current virtual environment (Python).
spaceship_venv_status() {
  [[ $SPACESHIP_VENV_SHOW == false ]] && return

  # Check if the current directory running via Virtualenv
  [ -n "$VIRTUAL_ENV" ] && $(type deactivate >/dev/null 2>&1) || return

  # Do not show venv prefix if prefixes are disabled
  #[[ $SPACESHIP_PREFIX_SHOW == true ]] && echo -n "${SPACESHIP_PREFIX_VENV}" || echo -n ' '

  echo -n "%{$fg_bold[blue]%}"
  echo -n "$(basename $VIRTUAL_ENV)"
  echo -n "%{$reset_color%}"
}

# Pyenv
# Show current version of pyenv python, including system.
spaceship_pyenv_status() {
  [[ $SPACESHIP_PYENV_SHOW == false ]] && return

  $(type pyenv >/dev/null 2>&1) || return # Do nothing if pyenv is not installed

  local pyenv_shell=$(pyenv shell 2>/dev/null)
  local pyenv_local=$(pyenv local 2>/dev/null)
  local pyenv_global=$(pyenv global 2>/dev/null)

  # Version follows this order: shell > local > global
  # See: https://github.com/yyuu/pyenv/blob/master/COMMANDS.md
  if [[ ! -z $pyenv_shell ]]; then
    pyenv_status=$pyenv_shell
  elif [[ ! -z $pyenv_local ]]; then
    pyenv_status=$pyenv_local
  elif [[ ! -z $pyenv_global ]]; then
    pyenv_status=$pyenv_global
  else
    return # If none of these is set, pyenv is not being used. Do nothing.
  fi

  # Do not show pyenv prefix if prefixes are disabled
  [[ $SPACESHIP_PREFIX_SHOW == true ]] && echo -n "${SPACESHIP_PREFIX_PYENV}" || echo -n ' '

  echo -n "%{$fg_bold[yellow]%}"
  echo -n "${SPACESHIP_PYENV_SYMBOL}  ${pyenv_status}"
  echo -n "%{$reset_color%}"
}


setopt extended_glob
theme_preexec () {
    if [[ "$TERM" == "screen" ]]; then
	local CMD=${1[(wr)^(*=*|sudo|-*)]}
	echo -n "\ek$CMD\e\\"
    fi
}


setprompt () {
    ###
    # Need this so the prompt will work.

    setopt prompt_subst


    ###
    # See if we can use colors.

    autoload zsh/terminfo
    for color in RED GREEN YELLOW BLUE MAGENTA CYAN WHITE GREY; do
	eval PR_$color='%{$terminfo[bold]$fg[${(L)color}]%}'
	eval PR_LIGHT_$color='%{$fg[${(L)color}]%}'
	(( count = $count + 1 ))
    done
    PR_NO_COLOUR="%{$terminfo[sgr0]%}"

    ###
    # Modify Git prompt
    ZSH_THEME_GIT_PROMPT_PREFIX="%{$fg[green]%}"
    ZSH_THEME_GIT_PROMPT_SUFFIX="%{$reset_color%}"
    ZSH_THEME_GIT_PROMPT_DIRTY="%{$FG[202]%}‚úò"
    ZSH_THEME_GIT_PROMPT_CLEAN="%{$FG[040]%}‚úî"

    ZSH_THEME_GIT_PROMPT_ADDED="%{$fg[green]%} ‚úö"
    ZSH_THEME_GIT_PROMPT_MODIFIED="%{$fg[blue]%} ‚úπ"
    ZSH_THEME_GIT_PROMPT_DELETED="%{$fg[red]%} ‚úñ"
    ZSH_THEME_GIT_PROMPT_RENAMED="%{$fg[magenta]%} ‚ûú"
    ZSH_THEME_GIT_PROMPT_UNMERGED="%{$fg[yellow]%} ‚ïê"
    ZSH_THEME_GIT_PROMPT_UNTRACKED="%{$fg[cyan]%} ‚ú≠"

    ###
    # See if we can use extended characters to look nicer.
    # UTF-8 Fixed

    if [[ $(locale charmap) == "UTF-8" ]]; then
	PR_SET_CHARSET=""
	PR_SHIFT_IN=""
	PR_SHIFT_OUT=""
	PR_HBAR="‚îÄ"
    GIT_PREFIX="g:"
        PR_ULCORNER="‚îå"
        PR_LLCORNER="‚îî"
        PR_LRCORNER="‚îò"
        PR_URCORNER="‚îê"
    else
        typeset -A altchar
        set -A altchar ${(s..)terminfo[acsc]}
        # Some stuff to help us draw nice lines
        PR_SET_CHARSET="%{$terminfo[enacs]%}"
        PR_SHIFT_IN="%{$terminfo[smacs]%}"
        PR_SHIFT_OUT="%{$terminfo[rmacs]%}"
        PR_HBAR='$PR_SHIFT_IN${altchar[q]:--}$PR_SHIFT_OUT'
        PR_ULCORNER='$PR_SHIFT_IN${altchar[l]:--}$PR_SHIFT_OUT'
        PR_LLCORNER='$PR_SHIFT_IN${altchar[m]:--}$PR_SHIFT_OUT'
        PR_LRCORNER='$PR_SHIFT_IN${altchar[j]:--}$PR_SHIFT_OUT'
        PR_URCORNER='$PR_SHIFT_IN${altchar[k]:--}$PR_SHIFT_OUT'
     fi


    ###
    # Decide if we need to set titlebar text.

    case $TERM in
	xterm*)
	    PR_TITLEBAR=$'%{\e]0;%(!.-=*[ROOT]*=- | .)%n@%m:%~ | ${COLUMNS}x${LINES} | %y\a%}'
	    ;;
	screen)
	    PR_TITLEBAR=$'%{\e_screen \005 (\005t) | %(!.-=[ROOT]=- | .)%n@%m:%~ | ${COLUMNS}x${LINES} | %y\e\\%}'
	    ;;
	*)
	    PR_TITLEBAR=''
	    ;;
    esac


    ###
    # Decide whether to set a screen title
    if [[ "$TERM" == "screen" ]]; then
	PR_STITLE=$'%{\ekzsh\e\\%}'
    else
	PR_STITLE=''
    fi


    ###
    # Finally, the prompt.

    PROMPT='$PR_SET_CHARSET$PR_STITLE${(e)PR_TITLEBAR}\
$PR_CYAN$PR_ULCORNER$PR_HBAR$PR_GREY(\
$PR_CYAN%(!.%SROOT%s.%n)$PR_GREY@$PR_GREEN%m\
$PR_GREY) $PR_GREY(\
$PR_GREEN%$PR_PWDLEN<...<%~%<<\
$PR_GREY) $PR_GREY(\
$PR_GREEN$(spaceship_venv_status)$(spaceship_pyenv_status)\
$PR_GREY`rvm_prompt_info || rbenv_prompt_info`)${(e)PR_FILLBAR}$PR_BLUE(\
$PR_LIGHT_BLUE%{$reset_color%}$PR_YELLOW$GIT_PREFIX`git_prompt_info``git_prompt_status`$PR_BLUE)$PR_CYAN$PR_HBAR\

$PR_CYAN$PR_LLCORNER$PR_CYAN$PR_HBAR\
‚û§$PR_NO_COLOUR '


#$PR_YELLOW%D{%H:%M:%S}\

#$PR_HBAR\


    # display exitcode on the right when >0
    return_code="%(?..%{$fg[red]%}%? ‚Üµ %{$reset_color%})"
    RPROMPT=' $return_code'
    ### ‰∏ãÈù¢‰∏§Ë°åÊòØreturn_code‰πãÂêéÁöÑ‰ª£Á†Å
    # $PR_CYAN$PR_HBAR$PR_BLUE$PR_HBAR\
    # ($PR_YELLOW%D{%a,%b%d}$PR_BLUE)$PR_HBAR$PR_CYAN$PR_LRCORNER$PR_NO_COLOUR

    PS2='$PR_CYAN$PR_HBAR\
$PR_BLUE$PR_HBAR(\
$PR_LIGHT_GREEN%_$PR_BLUE)$PR_HBAR\
$PR_CYAN$PR_HBAR$PR_NO_COLOUR '
}

setprompt

autoload -U add-zsh-hook
add-zsh-hook precmd  theme_precmd
add-zsh-hook preexec theme_preexec